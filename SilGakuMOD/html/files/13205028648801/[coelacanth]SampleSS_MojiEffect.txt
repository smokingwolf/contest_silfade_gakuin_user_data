#タイトル：サンプルSS「文字の演出」
#作者：シーラカンス（coelacanth.web@gmail.com）
#
#本サンプルSS作成にあたり、以下の情報を参考にさせていただきました。
#ありがとうございました。
#・WOLF RPGエディター 説明書（SmokingWOLF様）
# http://www.silversecond.com/WolfRPGEditor/Help/
#・「シル学」イベントエディターの"テビキ"（kiti様）
# http://sgedmanual.web.fc2.com/
#・SSテクニック集（カイル様）
# http://www.silversecond.com/game/SilGakuMOD/html/tdv207.html?#13166791751701


#■■■  タイトル設定  ■■■
!文字列操作 , ${文字列:シーラカンス/SSタイトル} , = , 文字の演出

#■■■  ここで一人称・主人公の呼び方をセット  ■■■
!文字列操作 , ${サラ:一人称} , = , 私
!文字列操作 , ${ファング:一人称} , = , 俺
!文字列操作 , ${アウル:一人称} , = , 私
!文字列操作 , ${ラクーン:一人称} , = , ボク


#■■■  タイトル表示  ■■■
!SE , ピーン
＠なし
「${文字列:シーラカンス/SSタイトル}」

!条件分岐 , ${システム:テストプレイ中？} , 1 , と同じ
!ジャンプ , test
!条件終了


#■■■  じっくり・すぐにモード選択  ■■■
!ラベル , モード選択
!エフェクト , フェードイン黒 , 0
!BGM , 意識の海
!移動 , 意識の海
!背景,Graphic\BackGround\星雲＋1.png,4,-2
!フォグ,Graphic\BackGround\星雲＋2.png,8,-4
!コモンイベント挿入,[開始イベント用コモンリスト],0,
!エフェクト , フェードアウト , 40
!ウェイト,10


＠なし
ご希望のモードを選択してください。
!選択肢初期化
!選択肢追加 , すぐに文字の演出を試す , すぐに ,  , 文字の演出をすぐに見られます。
!選択肢追加 , じっくり解説を見る , じっくり ,  , 体験版サラとトーテムによる解説SSをスタートします。
!選択肢追加 , 終了 , このSSを終了 ,  , このSSを終了します。
!選択肢実行 , 


#■■■  すぐにモード開始  ■■■
!ラベル , すぐに
!BGM , 
!エフェクト , フェードイン黒 , 60
!背景,
!フォグ,
!コモンイベント挿入,[開始イベント用コモンリスト],-1
!移動 , シルフェイド学院
!エフェクト , フェードアウト , 0

!ラベル , すぐにエフェクト選択
!選択肢初期化
!選択肢追加 , 文字表示の制御いろいろ ,  ,  , 
!選択肢追加 , 「心の声」以外でクリック待ち ,  ,  , 
!選択肢追加 , 既読ログと異なるセリフ ,  ,  , 
!選択肢追加 , タイプ入力風タイトル ,  ,  , 
!選択肢追加 , ドアップ連続文字タイトル ,  ,  , 
!選択肢追加 , 映画のスタッフロール風 ,  ,  , 
!選択肢追加 , モード選択に戻る ,  ,  , 
!選択肢実行 , 

#■■■  文字表示の制御いろいろ  ■■■
!ラベル , 文字表示の制御いろいろ
＠なし
ウディタの文字表示の制御を
いろいろ試してみます。

＠なし
「\c[1]￥f[数字]\c[0]」で
フォント\f[44]サイズ\f[22]変更。
＠なし
行頭に「\c[1]￥m[数字]\c[0]」で
\m[44]最大フォント\f[44]サイズ\f[22]指定。
\c[1]\f[16]（フォントサイズ変更の前後で高さが揃っています）
＠なし
「\c[1]￥c[数字]\c[0]」で
\c[4]フ\c[3]ォ\c[2]ン\c[1]ト\c[0]カ\c[4]ラ\c[3]ー\c[2]変\c[1]更\c[0]。
＠なし
\E「\c[1]￥E\c[0]」でフチ付き文字列。
\N「\c[1]￥N\c[0]」でフチ無し文字列。
＠なし
「\c[1]￥-[数字]\c[0]」で\-[5]文字幅を詰めます。
＠なし
「\c[1]￥font[数字]\c[0]」で
\font[2]フォントを変更します。
＠なし
\A+「\c[1]￥A+\c[0]」でアンチエイリアス有効。
\A-「\c[1]￥A-\c[0]」でアンチエイリアス解除。


＠なし
「\c[1]￥.\c[0]」で0.25秒\.ウェイト。
＠なし
\>「\c[1]￥>\c[0]」で文章の瞬間表示…………………
………………………………………………
「\c[1]￥<\c[0]」\<で瞬間表示の中断…………………
………………………………………………

＠なし
「\c[1]￥mx[数字]\c[0]」「\c[1]￥my[数字]\c[0]」で
文字のXY座標をずらしま\mx[-5]\my[10]す。
＠なし
「\c[1]￥ax[数字]\c[0]」「\c[1]￥ay[数字]\c[0]」で
文字のXY座標を指定しま\ax[250]\ay[35]す。
通常の文章が始まる座標を(0,0)と
しているようです。
＠なし
「\c[1]￥sp[数字]\c[0]」で\sp[5]文章の表示速度を変更。
＠なし
「\c[1]￥space[数字]\c[0]」で
\space[15]改行間隔を
変更します。
＠なし
「\c[1]￥r[文字,ルビ内容]\c[0]」で
\r[文字,もじ]に\r[ルビ,・・]を付けます。
＠なし
「\c[1]￥i[数字]\c[0]」で
アイテム・連絡先などで使われる
アイコンを表示します\i[138]
＠なし
「\c[1]￥img[ファイル名]\c[0]」で指定した画像を
文章中に表示します\img[Graphic\Icon\icon996.png]
＠なし
<L>「\c[1]＜L＞\c[0]」で左揃え。
<C>「\c[1]＜C＞\c[0]」で中央揃え。
<R>「\c[1]＜R＞\c[0]」で右揃え。
文章内で最長の行に合わせて揃えます。


＠なし
次に試す「\c[1]￥￥\c[0]」「\c[1]￥!\c[0]」「\c[1]￥^\c[0]」は
ウディタでは有効ですが、
シル学ユーザデータでは使えません。
＠なし
「\c[1]￥￥\c[0]」で半角の￥に変換。
\c[12]効きません\\。
＠なし
「\c[1]￥!\c[0]」で次のキー入力まで
文章表示を停止。\!
\c[12]効きません。
＠なし
\c[12]↑効かない上に、
「￥!」以降の文章が表示されません。
＠なし
「\c[1]￥^\c[0]」でキー入力を待たずに
文章表示を終了\^。
\c[12]効きません。

!ジャンプ , すぐにエフェクト選択
#■■■  文字表示の制御いろいろ  ■■■


#■■■  「心の声」以外でクリック待ち  ■■■
!ラベル , 「心の声」以外でクリック待ち
＠なし
（方法１）
「心の声」以外の文章表示でも
無理やり……
＠なし
\>（方法１）
「心の声」以外の文章表示でも
無理やり……\<クリック待ちができる！
＠なし
\>（方法１）
「心の声」以外の文章表示でも
無理やり……\<
\c[1]\f[16]↑瞬間表示を使用。既読ログが重複します。

!文字列操作 , ${文字列:TMP} , = , （方法２）\n「心の声」以外の文章表示でも\n無理やり……
＠なし
${文字列:TMP}
!文字列ピクチャ , 0 , 0 , \-[1]\space[7]\f[24]\A+${文字列:TMP} , 214 , 345 , 100 , 左上
＠なし
　
　
　　　　　　クリック待ちができる！
＠なし
　
　
　
\c[1]\f[16]↑実は文字列ピクチャ。クリックすると消えます。
!文字列ピクチャ , 0 , 0 , 消去 , 
!ジャンプ , すぐにエフェクト選択
#■■■  「心の声」以外でクリック待ち  ■■■



#■■■  既読ログと異なるセリフ  ■■■
!ラベル , 既読ログと異なるセリフ
＠なし
文字列ピクチャによって、
通常の文章を覆い隠します。

!文字列操作 , ${文字列:TMP} , = , 　　　　　　　　　\n　　　　　　　　　▲■
!文字列ピクチャ , 0 , 0 , \sp[200]\-[1]\space[7]\f[24]\A+${文字列:TMP} , 214 , 345 , 100 , 左上
＠なし
「ねー、知ってる？
　アウルはね、実は人間になれるんだよー」
（既読ログを見るとネタばれ）
＠なし
　
　
\c[1]\f[16]↑実は文字列ピクチャ。クリックすると消えます。
!文字列ピクチャ , 0 , 0 , 消去 , 

!文字列操作 , ${文字列:TMP} , = , 　　　　　　　　　　　　　　\n　　　　　　　　女
!文字列ピクチャ , 0 , 0 , \sp[200]\-[1]\space[7]\f[24]\A+${文字列:TMP} , 214 , 345 , 100 , 左上
＠なし
「ここだけの話なんだけど……
　エシュターって　だったんだよー」
（既読ログに残らないオフレコ）
＠なし
　
　
\c[1]\f[16]↑実は文字列ピクチャ。クリックすると消えます。
!文字列ピクチャ , 0 , 0 , 消去 , 
!ジャンプ , すぐにエフェクト選択
#■■■  既読ログと異なるセリフ  ■■■


#■■■  タイプ入力風タイトル  ■■■
!ラベル , タイプ入力風タイトル
!エフェクト , 暗くする , 

#■　表示文字列セット
!文字列操作 , ${文字列:TMP} , = , シークエンス．１
#■　画像ＩＤ設定（他の画像とＩＤがかぶらないように）
!変数操作 , ${変数:TMP} , = , 0
#■　表示Ｘ座標
!変数操作 , ${変数:TMP2} , = , 244
#■　表示Ｙ座標
!変数操作 , ${変数:TMP3} , = , 160

#■　一文字ずつタイプ風表示
!ラベル , タイプ入力風タイトル反復a1
!文字列操作 , ${文字列:TMP2} , →の1文字切出 , ${文字列:TMP}
!文字列操作 , ${文字列:TMP} , ←の1文字消去 , 
!文字列ピクチャ , ${変数:TMP} , 0 , ${文字列:TMP2} , ${変数:TMP2} , ${変数:TMP3} , 100 , 左上
!変数操作 , ${変数:TMP} , += , 1
!変数操作 , ${変数:TMP2} , += , 22
!SE , Sound\SE\ドア・カギ開け.ogg,50,60
!ウェイト , 10
!条件分岐 , ${文字列:TMP} ,  , 以外
!ジャンプ , タイプ入力風タイトル反復a1
!条件終了

!ウェイト , 40

#■　表示文字列セット
!文字列操作 , ${文字列:TMP} , = , 天まで届くエシュター
#■　画像ＩＤ設定（他の画像とＩＤがかぶらないように）
!変数操作 , ${変数:TMP} , = , 10
#■　表示Ｘ座標
!変数操作 , ${変数:TMP2} , = , 120
#■　表示Ｙ座標
!変数操作 , ${変数:TMP3} , = , 220

#■　一文字ずつタイプ風表示
!ラベル , タイプ入力風タイトル反復a2
!文字列操作 , ${文字列:TMP2} , →の1文字切出 , ${文字列:TMP}
!文字列操作 , ${文字列:TMP} , ←の1文字消去 , 
!文字列ピクチャ , ${変数:TMP} , 0 , \f[44]${文字列:TMP2} , ${変数:TMP2} , ${変数:TMP3} , 100 , 左上
!変数操作 , ${変数:TMP} , += , 1
!変数操作 , ${変数:TMP2} , += , 44
!SE , Sound\SE\ドア・カギ開け.ogg,50,60
!ウェイト , 10
!条件分岐 , ${文字列:TMP} ,  , 以外
!ジャンプ , タイプ入力風タイトル反復a2
!条件終了

!ウェイト , 60

#■　文字消去
!SE , Sound\SE\ドア・カギ開け.ogg,70,50
!ウェイト , 30
!ラベル , タイプ入力風タイトル消去反復a
!文字列ピクチャ , ${変数:TMP} , 50 , 消去 , 
!変数操作 , ${変数:TMP} , -= , 1
!条件分岐 , ${変数:TMP} , 0 , 以上
!ジャンプ , タイプ入力風タイトル消去反復a
!条件終了

!ウェイト , 10
!エフェクト , 普通にする , 
!ジャンプ , すぐにエフェクト選択
#■■■  タイプ入力風タイトル  ■■■


#■■■  ドアップ連続文字タイトル  ■■■
!ラベル , ドアップ連続文字タイトル
!エフェクト , 暗くする , 

#■　表示文字列セット
!文字列操作 , ${文字列:TMP} , = , 裸でうろつくイシュテナ先生の謎を追え！
!文字列操作 , ${文字列:TMP2} , = , ${文字列:TMP}

#■　一文字ずつドアップ表示
!ラベル , ドアップ連続文字タイトルa
!文字列操作 , ${文字列:TMP3} , →の1文字切出 , ${文字列:TMP2}
!文字列操作 , ${文字列:TMP2} , ←の1文字消去 , 
!文字列ピクチャ , 0 , 0 , \f[100]${文字列:TMP3} , 320 , 240 , 400 , 中心
!SE , Sound\SE\ドア・カギ開け.ogg,50,50
!ウェイト , 10
!条件分岐 , ${文字列:TMP2} ,  , 以外
!ジャンプ , ドアップ連続文字タイトルa
!条件終了

#■　ドーンと全体表示
!SE , Sound\SE\[戦闘]雷.wav , 60 , 70
!文字列ピクチャ , 0 , 0 , \f[33]${文字列:TMP} , 320 , 240 , 100 , 中心
!ウェイト , 60

#■　文字消去
!文字列ピクチャ , 0 , 30 , 消去 , 320 , 480 , 100 , 中心
!ウェイト , 30

!エフェクト , 普通にする , 
!ジャンプ , すぐにエフェクト選択
#■■■  ドアップ連続文字タイトル  ■■■


#■■■  映画のスタッフロール風  ■■■
!ラベル , 映画のスタッフロール風
!エフェクト , 暗くする , 
!文字列操作 , ${文字列:TMP} , = , <C>キャラクター人気投票　結果\n\n<L>第１位: ウリユ　<R>391点\n<L>第２位: 黒のサラ　<R>258点\n<L>第３位: メアリー　<R>231点\n<L>第４位: シーナ　<R>228点\n<L>第５位: ロベルト(教頭)　<R>175点\n<L>第６位: アルバート　<R>156点\n<L>第７位: アウル　<R>137点\n<L>第８位: セト　<R>132点\n<L>第９位: スケイル　<R>109点\n<L>第10位: ナダ　<R>82点
!文字列ピクチャ , 0 , 0 , ${文字列:TMP} , 320 , 600 , 100 , 中心
!文字列ピクチャ , 0 , 360 ,  , 320 , 240 , 100 , 中心
!ウェイト , 360

#■　クリック待ち
!変数操作 , ${変数:TMP} , = , 430
!変数操作 , ${変数:TMP2} , = , 0
!ラベル , クリック待ち表示a
!変数操作 , ${変数:TMP3} , = , ${変数:TMP2}
!変数操作 , ${変数:TMP3} , %= , 5
!条件分岐 , ${変数:TMP3} , 0 , と同じ
!ピクチャ , 1000 , 0 , Graphic\System\[Sys]メッセージ待ち.png , 645,${変数:TMP},100,右上
!!条件分岐 , ${変数:TMP2} , 10 , 未満
!変数操作 , ${変数:TMP} , += , 1
!!条件以外
!変数操作 , ${変数:TMP} , -= , 1
!!条件終了
!条件終了
!変数操作 , ${変数:TMP2} , += , 1
!条件分岐 , ${変数:TMP2} , 20 , 以上
!変数操作 , ${変数:TMP2} , = , 0
!条件終了
!ウェイト , 1
!条件分岐 , ${システム:マウス左クリック状態} , 0 , と同じ
!ジャンプ , クリック待ち表示a
!条件以外
!ピクチャ , 1000 , 0 , 消去 , 
!条件終了
#■　クリック待ち

!文字列ピクチャ , 0 , 60 , 消去 , 320 , 240 , 100 , 中心
!ウェイト , 60

!エフェクト , 普通にする , 
!ジャンプ , すぐにエフェクト選択
#■■■  映画のスタッフロール風  ■■■



!ラベル , モード選択に戻る
!ジャンプ , モード選択



#■■■  じっくりモード開始  ■■■
!ラベル , じっくり
!ウェイト,20
＠心の声
意識の海にただよう、そこのあなた……
私の声が、聞こえますか……？

!ウェイト,20
!BGM , コミカル2
＠サラ
冒頭メッセージの「ただよう」を
じっとみていると、一瞬、
この言葉が正しいかどうか、
自信無くなるんですよね……。
＠サラ＠笑顔
あ、こんにちは。
数ある並列世界のうち、
\c[1]体験版世界のサラ\c[0]です。
＠ファング＠困る
別世界から主人公を呼びつけたあげく、
お金も渡さずパシリにして
ゲームを買ってこさせた\r[方,ほう]のサラだ。
＠サラ＠笑顔
（無視）体験版の際には、
シルフェイド学院物語を買って頂き、
ありがとうございました！
＠サラ＠笑顔
あれから、
\f[40]120時間\f[22]くらいやってまーす！
＠アウル＠笑顔
プレイ時間、
また一段と増えましたね……。
＠サラ＠笑顔
ええ。最近では、
自作のユーザデータなんかも
作ってますよ。
＠ラクーン＠笑顔
ちなみに、サラさんは
どんなデータを作ってるの？
＠サラ＠笑顔
ふふふ、
よくぞ聞いてくださいました！
＠サラ＠笑顔
\c[1]\f[33]「体験版サラ強化データ」
\c[0]\f[22]というものです！
＠アウル＠困る
え？
サラさん自身のデータですか？
＠サラ＠笑顔
ええ。
\meのイベントが、
ちょっと少なく感じたので。
＠サラ＠笑顔
内容としては、\meに
シルフェイド学院物語だけでなく、
\c[1]オレンジジュース\c[0]や\c[1]ポテチ\c[0]を買ってくる
イベントなど……。
!SE , バシュー
＠ファング＠ガーン
\f[33]もう自分で買ってこいよ！！
＠サラ＠困る
でも、買ってこないと
災いが発生して、みんな消えてしまいます。
＠ラクーン＠ガーン
\f[33]ええーー！？
＠サラ＠困る
そのドラマチックな物語を
演出するために、
いろいろ工夫しているのですが……。
＠アウル＠ガーン
た、確かに、
買い物と世界消滅を結びつけるには、
相当のドラマが必要そうですが……。
＠サラ＠笑顔
今回は、
そんな物語の演出に役立つかもしれない、
\c[1]文字に関するテクニック\c[0]をご紹介します。

!ウェイト , 20
!BGM , 穏やか1
＠サラ＠笑顔
まず、文章表示で重要な、
「間」のテクニックです！
＠サラ
画面中段に表示される「心の声」では、
文中に「@」を入れた位置で
クリック待ちをさせる事が出来ます。
＠心の声
私が大好きなポテチの味、
それは……@サワークリームオニオン！
＠サラ
このように、
重要な言葉の前に一呼吸置いて、
強調することが出来ます。
＠ファング
今の例えは、
限りなくどうでもいい
情報ではあるがな。
＠サラ＠困る
ところが、「心の声」以外のセリフでは
この「@」が効かないのです。
＠サラ＠怒り
私が大好きなポテチの味、
それは……@サワークリームオニオン！
＠サラ＠困る
……全然かっこよくないですよね。
＠ファング＠困る
演出の問題か……これ。
＠ラクーン＠困る
しーっ！
＠サラ＠笑顔
しかし、今から使う方法なら、
\c[1]普通の文章表示でクリック待ち\c[0]が
使えるのです。
＠サラ
行きますよ……。

#■■■  「心の声」以外でクリック待ち方法１  ■■■
＠サラ＠怒り
私が大好きなポテチの味、
それは……
＠サラ＠怒り
\>私が大好きなポテチの味、
それは……\<サワークリームオニオン！
#■■■  「心の声」以外でクリック待ち方法１  ■■■

＠サラ＠笑顔
……どうですか？
＠アウル＠笑顔
おおー！
どうやったんですか？
＠ラクーン＠困る
あれ、既読ログを見ると、
クリック前の文章が二回言われてる！
＠サラ＠笑顔
この方法では、まずクリック前の
文章を普通に表示して……
＠サラ
次の文章で、
\c[1]クリック前までを「￥>」で瞬間表示\c[0]、
続きの文章から
\c[1]「￥<」で瞬間表示を中断\c[0]しています。
＠サラ＠笑顔
すると、クリック待ちのように
見えるわけです。
＠ファング＠困る
この方法は簡単ではあるが、
既読ログで文章がダブってしまうのが
欠点だな。
＠サラ＠笑顔
この欠点を改良したのが、
次のやり方です。
!ウェイト , 30

#■■■  「心の声」以外でクリック待ち方法２  ■■■
!文字列操作 , ${文字列:TMP} , = , 私が大好きなポテチの味、\nそれは……
＠サラ＠怒り
${文字列:TMP}
!文字列ピクチャ , 0 , 0 , \-[1]\space[7]\f[24]\A+${文字列:TMP} , 214 , 345 , 100 , 左上
＠サラ＠怒り
　
　　　　　サワークリームオニオン！
!文字列ピクチャ , 0 , 0 , 消去 , 
#■■■  「心の声」以外でクリック待ち方法２  ■■■

＠サラ＠笑顔
……いかがでしょう？
＠アウル＠笑顔
今度も成功しましたね！
＠ラクーン＠困る
既読ログを見ると、
今度は二つの文に分離してる！
＠サラ＠笑顔
ここでやっていることをまとめると……
次の通りです。

１．クリック前の文章を表示

２．クリック前の文章が消える瞬間、
　　「\c[1]文字列ピクチャ\c[0]」で
　　ぴったり同じ文字列を表示

３．クリック前文章の入るスペースを空けて
　　クリック後文章を表示

これで、あたかも
クリック待ちが発生しているように
見えます。
＠ファング＠笑顔
\r[力,ちから]\r[技,わざ]だな……。
＠サラ＠笑顔
以下のコマンドで、＄{文字列:TMP}の中身を
文章表示にぴったり重ねられます。
\c[1]\f[16]!文字列ピクチャ,0,0,
￥-[1]￥space[7]￥f[24]￥A+＄{文字列:TMP},214,345,100,左上
\c[0]（実際の命令は一行で、￥＄は半角文字）
＠アウル＠困る
文字列ピクチャの表示文字列の部分の、
「￥-[1]￥space[7]￥f[24]￥A+」
という、ややこしい部分は何ですか？
＠サラ
これは、文章表示にぴったり重ねるために、
文字間隔・行間・サイズなどを
調整するためのものです。
＠サラ＠笑顔
１から調整するのは時間がかかりますが、
コピペして使えば手軽だと思います！
＠ラクーン＠笑顔
「ぴったり重なる文字列ピクチャ」
……他にも使い道が
あるかも知れないね。
＠サラ＠笑顔
そうですね。
文字列ピクチャは通常の文章と違い
\c[1]既読ログに残らない\c[0]ので……。

#■■■  既読ログと異なるセリフ１  ■■■
!文字列操作 , ${文字列:TMP} , = , 　　　　\n　　●　　　　●
!文字列ピクチャ , 0 , 0 , \sp[200]\-[1]\space[7]\f[24]\A+${文字列:TMP} , 214 , 345 , 100 , 左上
＠サラ＠笑顔
例えば、
「ウマコ！　ウマコ！」
とか言っても……。
!文字列ピクチャ , 0 , 5 , 消去 , 
#■■■  既読ログと異なるセリフ１  ■■■

＠アウル＠ガーン
ちょっとーー！？
いきなり何言ってるんですかーー！？
＠サラ＠笑顔
いやですねぇ。
既読ログを見てくださいよ。
＠ラクーン＠笑顔
なあんだ、ウマコだったんだね！
それなら安心！！
＠ファング＠困る
……。

#■■■  既読ログと異なるセリフ２  ■■■
!文字列操作 , ${文字列:TMP} , = , 　　　　\n　　　　黒サラ　　　　\n　シーナ　地歴編　　助かる
!文字列ピクチャ , 0 , 0 , \sp[200]\-[1]\space[7]\f[24]\A+${文字列:TMP} , 214 , 345 , 100 , 左上
＠サラ＠笑顔
他にも、
「犯人は　　　」とか、
「　　　は　　　では　　　」とか……。
!文字列ピクチャ , 0 , 0 , 消去 , 
#■■■  既読ログと異なるセリフ２  ■■■

＠アウル＠ガーン
ひどいですよ！？
\me、まだシル学
途中までしかやってないのに！
＠サラ＠笑顔
もう、アウル。
何を言ってるんですか。
既読ログをチェックですよ。
＠ラクーン＠笑顔
あれ、肝心なところは
ちゃんと伏せてあるね！
＠アウル＠笑顔
すみません。
\me、ちょっと聞き間違えたみたいです。
＠ファング＠ガーン
……。

＠サラ
他にも、文字列ピクチャは
\c[1]画面上に自由に配置\c[0]することができます。
\c[1]出現のタイミングも自由\c[0]です。

＠サラ
また、文字列ピクチャは
\c[1]文字の移動・拡大縮小・
フェードイン／アウト\c[0]など
いろいろできるんですよ。

＠サラ
\meのユザデでは、
文字列ピクチャを使って
少し\r[凝,こ]ったオープニングを
作っています。

!BGM , 
!エフェクト , フェードイン黒 , 30
!背景,
!フォグ,
!コモンイベント挿入,[開始イベント用コモンリスト],-1
!移動 , 屋上 , 特殊
!エフェクト , 暗くする , 0
!エフェクト , フェードアウト , 30

!BGM , タイトル
!ウェイト , 30

＠心の声
『パンがなければ、
　　　　　お菓子を食べればいいじゃない』@
　
　　　　　　　　　　　　　　　　……ある人が、そう言った。
＠心の声
でも、私の手元には、
　　　　　パンもお菓子も、無かったのだ。
＠心の声
だれか……買ってきてくれないかなぁ……。
　　　　　（ついでに面白いゲームも一つ欲しいなぁ）@
　
　　　　　　　　　　　　　　　　　　　　　サラ・アブシール

!ウェイト , 40

#■■■  タイプ入力風タイトル  ■■■

#■　表示文字列セット
!文字列操作 , ${文字列:TMP} , = , シークエンス．１
#■　画像ＩＤ設定（他の画像とＩＤがかぶらないように）
!変数操作 , ${変数:TMP} , = , 0
#■　表示Ｘ座標
!変数操作 , ${変数:TMP2} , = , 244
#■　表示Ｙ座標
!変数操作 , ${変数:TMP3} , = , 160

#■　一文字ずつタイプ風表示
!ラベル , タイプ入力風タイトル反復b1
!文字列操作 , ${文字列:TMP2} , →の1文字切出 , ${文字列:TMP}
!文字列操作 , ${文字列:TMP} , ←の1文字消去 , 
!文字列ピクチャ , ${変数:TMP} , 0 , ${文字列:TMP2} , ${変数:TMP2} , ${変数:TMP3} , 100 , 左上
!変数操作 , ${変数:TMP} , += , 1
!変数操作 , ${変数:TMP2} , += , 22
!SE , Sound\SE\ドア・カギ開け.ogg,50,60
!ウェイト , 10
!条件分岐 , ${文字列:TMP} ,  , 以外
!ジャンプ , タイプ入力風タイトル反復b1
!条件終了

!ウェイト , 40

#■　表示文字列セット
!文字列操作 , ${文字列:TMP} , = , 静かなる買物
#■　画像ＩＤ設定（他の画像とＩＤがかぶらないように）
!変数操作 , ${変数:TMP} , = , 10
#■　表示Ｘ座標
!変数操作 , ${変数:TMP2} , = , 200
#■　表示Ｙ座標
!変数操作 , ${変数:TMP3} , = , 220

#■　一文字ずつタイプ風表示
!ラベル , タイプ入力風タイトル反復b2
!文字列操作 , ${文字列:TMP2} , →の1文字切出 , ${文字列:TMP}
!文字列操作 , ${文字列:TMP} , ←の1文字消去 , 
!文字列ピクチャ , ${変数:TMP} , 0 , \f[44]${文字列:TMP2} , ${変数:TMP2} , ${変数:TMP3} , 100 , 左上
!変数操作 , ${変数:TMP} , += , 1
!変数操作 , ${変数:TMP2} , += , 44
!SE , Sound\SE\ドア・カギ開け.ogg,50,60
!条件分岐 , ${変数:TMP} , 14 , と同じ
!ウェイト , 40
!条件分岐 , ${変数:TMP} , 15 , と同じ
!ウェイト , 30
!条件以外
!ウェイト , 10
!条件終了
!条件分岐 , ${文字列:TMP} ,  , 以外
!ジャンプ , タイプ入力風タイトル反復b2
!条件終了

!ウェイト , 60

#■　文字消去
!SE , Sound\SE\ドア・カギ開け.ogg,70,50
!ウェイト , 30
!ラベル , タイプ入力風タイトル消去反復b
!文字列ピクチャ , ${変数:TMP} , 50 , 消去 , 
!変数操作 , ${変数:TMP} , -= , 1
!条件分岐 , ${変数:TMP} , 0 , 以上
!ジャンプ , タイプ入力風タイトル消去反復b
!条件終了

!ウェイト , 60
#■■■  タイプ入力風タイトル  ■■■

!エフェクト , 普通にする , 
!BGM , 
!BGM , コミカル1
＠ファング＠ガーン
\f[44]なんだこれ！？
＠サラ＠笑顔
あの、いかがでしたかっ？
かっこいいでしょ！？
＠アウル＠ガーン
ええ、作りこんでますね。
（演出は）
＠ラクーン＠困る
なんで背景が、
夕方の屋上なの？
＠サラ＠笑顔
\f[33]だって、きれいですから！
＠ラクーン＠困る
そっかー……。

＠サラ＠笑顔
この、シブい冒頭から
私のユザデは始まります。
＠サラ＠笑顔
そして中盤、
物語は大きく動くのです！


!BGM , 
!エフェクト , フェードイン白 , 60

!BGM , 意識の海
!ウェイト , 30
＠なし
……主人公さん。
……主人公さん。
!ウェイト , 20
＠サラ
今日は、主人公さんに
大切なことを伝えに来ました。

!BGM ,
!BGM , ボス戦2
!ウェイト , 30
#■■■  ドアップ連続文字タイトル  ■■■
!エフェクト , 暗くする , 

#■　表示文字列セット
!文字列操作 , ${文字列:TMP} , = , この後、サラの口から大変なことが！？
!文字列操作 , ${文字列:TMP2} , = , ${文字列:TMP}

#■　一文字ずつドアップ表示
!ラベル , ドアップ連続文字タイトルb
!文字列操作 , ${文字列:TMP3} , →の1文字切出 , ${文字列:TMP2}
!文字列操作 , ${文字列:TMP2} , ←の1文字消去 , 
!文字列ピクチャ , 0 , 0 , \f[100]${文字列:TMP3} , 320 , 240 , 400 , 中心
!SE , Sound\SE\ドア・カギ開け.ogg,50,50
!ウェイト , 10
!条件分岐 , ${文字列:TMP2} ,  , 以外
!ジャンプ , ドアップ連続文字タイトルb
!条件終了

#■　ドーンと全体表示
!SE , Sound\SE\[戦闘]雷.wav , 60 , 70
!文字列ピクチャ , 0 , 0 , \f[33]${文字列:TMP} , 320 , 240 , 100 , 中心
!ウェイト , 60

#■　文字消去
!文字列ピクチャ , 0 , 30 , 消去 , 320 , 480 , 100 , 中心
!ウェイト , 30

!エフェクト , 普通にする , 
#■■■  ドアップ連続文字タイトル  ■■■

＠サラ
追加で、\c[1]漫画も１〜２冊\c[0]
買ってきてもらえますか？

!BGM , 
!BGM , コミカル1
!SE , Sound\SE\[戦闘]雷.wav , 60 , 70
＠ファング＠ガーン
\f[44]ショボいわ！
＠サラ＠笑顔
あ、これ買わないと
バッドエンドですから。
＠アウル＠ガーン
\f[44]ひーー！？
＠ラクーン＠困る
ある意味、演出に相応しい
びっくり展開だけど……。

＠サラ＠笑顔
そしてラスト！
最後の最後で苦労が\r[報,むく]われ、
感動の大団円です！


!ラベル , test
!BGM , 
!移動 , 上空
!エフェクト , 暗くする , 
!エフェクト , フェードアウト , 30

!BGM , エンド1
#■■■  映画のスタッフロール風  ■■■
!文字列操作 , ${文字列:TMP} , = , <C>こうして、シルフェイド島は……\n\nいや、世界は、災いから救われた。\n\n壮絶な買い物であった。\n\nコンビニ強盗に果敢に挑み、見事討ち死にした、ファング。\n\n知力を生かして、地歴バイトを発狂するまでやってくれた、アウル。\n\n値切りすぎて、ユーミス雑貨店から出入り禁止をくらった、ラクーン。\n\nみんなのことは、忘れない――。
!文字列ピクチャ , 0 , 0 , ${文字列:TMP} , 320 , 650 , 100 , 中心
!文字列ピクチャ , 0 , 1230 ,  , 320 , 240 , 100 , 中心
!ウェイト , 1230

#■　クリック待ち
!変数操作 , ${変数:TMP} , = , 430
!変数操作 , ${変数:TMP2} , = , 0
!ラベル , クリック待ち表示b
!変数操作 , ${変数:TMP3} , = , ${変数:TMP2}
!変数操作 , ${変数:TMP3} , %= , 5
!条件分岐 , ${変数:TMP3} , 0 , と同じ
!ピクチャ , 1000 , 0 , Graphic\System\[Sys]メッセージ待ち.png , 645,${変数:TMP},100,右上
!!条件分岐 , ${変数:TMP2} , 10 , 未満
!変数操作 , ${変数:TMP} , += , 1
!!条件以外
!変数操作 , ${変数:TMP} , -= , 1
!!条件終了
!条件終了
!変数操作 , ${変数:TMP2} , += , 1
!条件分岐 , ${変数:TMP2} , 20 , 以上
!変数操作 , ${変数:TMP2} , = , 0
!条件終了
!ウェイト , 1
!条件分岐 , ${システム:マウス左クリック状態} , 0 , と同じ
!ジャンプ , クリック待ち表示b
!条件以外
!ピクチャ , 1000 , 0 , 消去 , 
!条件終了
#■　クリック待ち

!文字列ピクチャ , 0 , 60 , 消去 , 320 , 240 , 100 , 中心
!ウェイト , 60

!エフェクト , 普通にする , 
#■■■  映画のスタッフロール風  ■■■
＠サラ＠笑顔
ありがとう、みんな！
!BGM , 
!BGM , コミカル1
＠ラクーン＠ガーン
\f[33]\r[犠,ぎ]\r[牲,せい]出しすぎだよ！！
＠アウル＠ガーン
どんな買い物をしたら
こんな結末になるんですか！？
＠ファング＠ガーン
\f[33]というか、
勝手に殺すな！！
＠サラ＠笑顔
このように、\c[1]文字列ピクチャ\c[0]によって
いろいろなシーンを演出してみました。
＠サラ＠困る
ただ、一つ問題が……。
＠ラクーン＠困る
一つ……？
＠サラ＠困る
この序盤・中盤・終盤を、
どう\r[繋,つな]げるかが、難しいんですよね……。
＠アウル＠ガーン
\f[33]確かに、難しそうですね！？
＠サラ＠笑顔
一年間、中だるみしないように、
買い物イベントをたくさん作らないと……。
＠ラクーン＠ガーン
\f[33]そんな一年やだーー！！
＠ファング＠ガーン
サラ、悪いことは言わん。
この演出を生かして、
別のユザデを作ろう、な？
＠サラ＠笑顔
……。

#■■■  既読ログと異なるセリフ２  ■■■
!文字列操作 , ${文字列:TMP} , = , ……チッ。
!文字列ピクチャ , 0 , 0 , \sp[200]\-[1]\space[7]\f[24]\A+${文字列:TMP} , 214 , 345 , 100 , 左上
＠サラ＠笑顔
　　　
!文字列ピクチャ , 0 , 0 , 消去 , 
#■■■  既読ログと異なるセリフ２  ■■■

＠ファング＠ガーン
今、舌打ちしただろ！？
＠サラ＠笑顔
舌打ちしてないですし。
証拠でもあるんですか？
＠ファング＠ガーン
\f[33]ああーーー！？
既読ログがーーー！？
＠サラ＠笑顔
ニヤリ。
＠アウル＠笑顔
それでは、今回はこの辺で
お別れです。
＠ファング＠ガーン
待て！
こいつの根性をたたき直す！
＠ラクーン＠困る
そんなことすると、
どんどん扱いが悪くなるよ……。
＠アウル＠笑顔
え、えーと、それでは
またお会いしましょう！
＠サラ＠笑顔
シーユー！
＠ファング＠ガーン
まーてー！

!エフェクト , フェードイン黒 , 30
!移動 , なし
!ジャンプ , モード選択

!ラベル , このSSを終了
